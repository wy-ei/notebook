# Cypher 基础教程

## Cypher 基本概念

Cypher 是 Neo4j 数据库的查询语言，就如同 SQL 之于其他关系型数据库一样。Neo4j 作为一种图数据库，其数据均以节点、关系来存储。所以 Cypher 应该能够有某种语法来描述节点和关系，并能表征他们之间的关系。


比如下面 cypher 用来查找所有居住在北京的人。

```cypher
(p:Person) -[:LIVES_IN]-> (:City {name: 'Beijing'}) return p
```

### 节点语法

```cypher
() # 匿名的节点
(matrix) # 使用一个变量 matrix 与这个节点关联
(:Movie) # 类型为 Movie 的节点
(matrix:Movie)
(matrix:Movie {title: "The Matrix"}) # 指含有特定属性的某类节点
(matrix:Movie {title: "The Matrix", released: 1997})
```

### 关系语法

```cypher
--> # 非直接相连的关系
-[role]-> # 使用变量关联此关系
-[:ACTED_IN]-> # 类型为 ACTED_IN 的关系
-[role:ACTED_IN]->
-[role:ACTED_IN {roles: ["Neo"]}]-> # 含有特定属性的关系
```

### 模式语法

将节点和关系组合起来，得出一个模式，而后使用此模式进行匹配：

```cypher
(keanu:Person:Actor {name:  "Keanu Reeves"} )
-[role:ACTED_IN     {roles: ["Neo"] } ]->
(matrix:Movie       {title: "The Matrix"} )
```

### 模式变量

模式由多个节点和关系组成，通常较长，可以将其保存为一个变量。在一条 cypher 中的其他位置，便可以使用此模式。

```cypher
acted_in = (:Person)-[:ACTED_IN]->(:Movie)
```

### 子句

如同 SQL 中的 `SELECT`、`WHERE` 等子句，在 cypher 中也有这类子句，用来进行查找、过滤、排序等操作。


### 创建

使用 `CREATE` 关键字能够创建节点、关系、模式，如下面语句，创建了一个类型为 `Movie` 的节点，且含有两个属性：

```cypher
CREATE (:Movie { title:"The Matrix",released:1997 })
```

还可以同时使用多个 `CREATE` 创建更为复杂的模式：

```cypher
CREATE (a:Person { name:"Tom Hanks",
  born:1956 })-[r:ACTED_IN { roles: ["Forrest"]}]->(m:Movie { title:"Forrest Gump",released:1994 })
CREATE (d:Person { name:"Robert Zemeckis", born:1951 })-[:DIRECTED]->(m)
RETURN a,d,r,m
```

![](https://neo4j.com/docs/developer-manual/current/images/cypherdoc-result-b3411d1d.svg)


为现有的节点添加关系：

```cypher
MATCH (p:Person { name:"Tom Hanks" })
CREATE (m:Movie { title:"Cloud Atlas",released:2012 })
CREATE (p)-[r:ACTED_IN { roles: ['Zachry']}]->(m)
RETURN p,r,m
```

![](https://neo4j.com/docs/developer-manual/current/images/cypherdoc-result-36eb9048.svg)


### 匹配

```cypher
MATCH (m:Movie)
RETURN m


MATCH (p:Person { name:"Keanu Reeves" })
RETURN p


MATCH (p:Person { name:"Tom Hanks" })-[r:ACTED_IN]->(m:Movie)
RETURN m.title, r.roles
```

### Merge

有时候希望给某个节点添加属性，但又不能保证其存在于库中，此时可以使用 `MERGE`。

首先查找某个模式，如果存在便得出这个模式，不存在则创建。在 `ON CREATE` 中指定的操作，会在创建的时候进行。

```cypher
MERGE (m:Movie { title:"Cloud Atlas" })
ON CREATE SET m.released = 2012
RETURN m
```

如果不存在 `p` 和 `m` 之间的 `ACTED_IN` 关系，则创建，并在创建时添加属性。

```cypher
MATCH (m:Movie { title:"Cloud Atlas" })
MATCH (p:Person { name:"Tom Hanks" })
MERGE (p)-[r:ACTED_IN]->(m)
ON CREATE SET r.roles =['Zachry']
RETURN p,r,m
```

![](https://neo4j.com/docs/developer-manual/current/images/cypherdoc-result-36eb9048.svg)


## 得到正确的结果

![](https://neo4j.com/docs/developer-manual/current/images/cypherdoc--ee5ee0f8.svg)

### 过滤结果

可以在 `WHERE` 中对 `MATCH` 的结果进行过滤：

```cypher
MATCH (m:Movie)
WHERE m.title = "The Matrix"
RETURN m
```

更好的方式是，在 `MATCH` 中指定更细致的条件：

```cypher
MATCH (m:Movie { title: "The Matrix" })
RETURN m
```

`WHERE` 子句中可以使用更复杂的条件：


```cypher
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name =~ "K.+" OR m.released > 2000 OR "Neo" IN r.roles
RETURN p,r,m
```

在 `WHERE` 中还可以指定一个模式，可以过滤掉符合或者不符合这个模式的结果。

```cypher
MATCH (p:Person)-[:ACTED_IN]->(m)
WHERE NOT (p)-[:DIRECTED]->()
RETURN p,m
```

### 返回结果

可以对结果整体来处理：

```cypher
MATCH (:Person)
RETURN count(*) AS people
```

```
+--------+
| people |
+--------+
| 3      |
+--------+
1 row
```

使用 `DISTINCT` 滤除重复: `count(DISTINCT role)`


`count` 会将其他列来进行分组，下面例子中会使用 `actor,director` 作为分组的键值：

```
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)<-[:DIRECTED]-(director:Person)
RETURN actor,director,count(*) AS collaborations
```

### 对结果排序

`ORDER BY` 可以基于任何可以访问到的变量、属性进行排序，默认是正序，如需倒序，使用关键词 `DESC`：

```
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
RETURN a,count(*) AS appearances
ORDER BY appearances DESC;
```

### Collect

对于 parent-children 形式的数据组成结构，如果要查出 parent 和所有 child，那么相同的 parent 会重复多次。使用 `collect` 可以将所有 child 收集到一个数组中。

```cypher
MATCH(u:求职者)-[d:浏览]->(r:招聘公告) return u.id, collect(r.id)
```

## 组合多条查询语句

### UNION

`UNION` 可以将两个查询结果合并起来：

```cypher
MATCH (actor:Person)-[r:ACTED_IN]->(movie:Movie)
RETURN actor.name AS name, type(r) AS acted_in, movie.title AS title
UNION
MATCH (director:Person)-[r:DIRECTED]->(movie:Movie)
RETURN director.name AS name, type(r) AS acted_in, movie.title AS title
```

```
+-------------------------------------------------+
| name              | acted_in   | title          |
+-------------------------------------------------+
| "Tom Hanks"       | "ACTED_IN" | "Cloud Atlas"  |
| "Tom Hanks"       | "ACTED_IN" | "Forrest Gump" |
| "Robert Zemeckis" | "DIRECTED" | "Forrest Gump" |
+-------------------------------------------------+
3 rows
```

### WITH

`WITH` 能将对个语句连接起来，就像管道一样，前一个语句的输出作为下一个语句的输入：

```cypher
MATCH (person:Person)-[:ACTED_IN]->(m:Movie)
WITH person, count(*) AS appearances, collect(m.title) AS movies
WHERE appearances > 1
RETURN person.name, appearances, movies
```


## Cypher 语法介绍

Cypher 语法结构的设计借鉴了 SQL，包含：

- `MATCH`: 用来匹配一个模式
- `WHERE`: 用来对模式进行过滤
- `RETURN`: 返回结果

下面是一个例子：

![](https://neo4j.com/docs/developer-manual/current/images/Example-Graph-cypher-intro.svg)


根据上图中的关系，现在需要查找 John 的所有朋友的朋友，并返回 John 和其朋友的朋友的姓名。

```cypher
MATCH (john {name: 'John'})-[:friend]->()-[:friend]->(fof)
RETURN john.name, fof.name
```

返回结果：

```
+----------------------+
| john.name | fof.name |
+----------------------+
| "John"    | "Maria"  |
| "John"    | "Steve"  |
+----------------------+
2 rows
```

现在加入 `WHERE` 子句，对 `MATCH` 的结果进行处理：

```cypher
MATCH (user)-[:friend]->(follower)
WHERE user.name IN ['Joe', 'John', 'Sara', 'Maria', 'Steve'] AND follower.name =~ 'S.*'
RETURN user.name, follower.name
```

返回结果：

```
+---------------------------+
| user.name | follower.name |
+---------------------------+
| "Joe"     | "Steve"       |
| "John"    | "Sara"        |
+---------------------------+
2 rows
```

`WHERE` 限制 `user.name` 的取值范围，并限制了 follower 的 name 只能以 `S` 开头。